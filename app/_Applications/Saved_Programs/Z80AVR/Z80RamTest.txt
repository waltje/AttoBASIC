5 REM Z80 RAM ERROR TEST
10# REM PD2=IORQ, PD3=MREQ, PD4=WR, PD5=RD
15# REM PB4=Z80CLK, PE2=Z80RST, PD6=Z80BUSRQ
20# REM PF1/PF0 = S1/S0 FOR WS-GEN
25# REM PB0=HC299_CS, PB6=HC299_OE, PB7=HC299_S1
30 SPM 2# SPI TO MODE 2
35 D:= $55; Z:=0# DATA TO WRITE=$55, Z HOLDS '0'
40 GOS 225# INSURE ALL PORTS AS INPUTS
45 GOS 235# START Z80 CLOCK AND INITIATE A Z80 RST
50 SDD6; CBD6# INITIATE Z80 BUSRQ
55 IF 1 = IBE0# WAIT FOR Z80 BUSACK
60 GOTO 55# LOOP TILL Z80 BUSACK
65 ODA $FF; OPA $00# SET PORTA OUTPUT AND ADDRESS A[7:0] =0x00
70 ODC $FF; OPC $00# SET PORTC OUTPUT AND ADDRESS A[15:8] =0x00
75 OPB '11010001; ODB '11010111# SS=1 (SEL HC299), OE=1 (HI-Z), (S0=1/S1=1)
80 ODD '10111100; OPD '11111111# CONTROL PORT TO OUTPUTS, ALL HIGH
85 ODE '00000100; OPE '11111111# SET PORTE, INPUTS W/ PULL-UPS
90 ODF '00000011; OPF '11111100# SET S0/S1 OUTPUTS FOR 0 WS
95 RTI 2; RTR# SET 100MS RTC INTERVAL AND RESET THE RTC
100 FOR U=0 255; FOR L=0 255# SET UP THE UPPER AND LOWER ADDRESS LOOPS
105 OPC U; OPA L# OUTPUT CURRENT ADDRESS ONTO BUS
110 GOS 165# WRITE DATA TO THE RAM ADDRESS
115 GOS 180# READ DATA FROM THE RAM ADDRESS AND PRINT IT
120 SBD5; SBD3# RAISE THE Z80 RD AND MREQ SIGNAL
125 IF X != D# RESULT = EXECTED?
130 GOS 195# ERROR, INFORM USER
135 D:= COM D# COMPLIMENT DATA TO WRITE (ODD/EVEN ADDR)
140 NEXT L; NEXT U# CONTINUE INCREMENT LOOP
145 PRI PEEK VPG@RTC1; PRI PEEK VPG@RTC0# PRINT THE CONTENTS OF THE RTC (IN 0.1 SEC)
150 ODA $FF; ODB $FF; ODC $FF; ODD $FF# SET ALL PORTS TO INPUTS
155 END
160# *** WRITE DATA TO DATA BUS ***
165 CBB7; SPW D; CBB6; CBD3# SHIFT ENABLE, WRITE DATA='D', ENABLE DATA ONTO BUS, DROP Z80 MREQ
170 CBD4; SBD4; SBD3; SBB6; SBB7; RET# PULSE Z80 WR, RAISE Z80 MREQ, OE=1,S1=1 THEN RETURN
175# *** READ DATA FROM DATA BUS ***
180 CBD3; CBD5; SPW 0; SBD5; SBD3# DROP Z80 MREQ, DROP Z80 RD, DUMMY WRITE TO LOAD, RAISE Z80 RD, RAISE Z80 MREQ
185 CBB7; X:= SPR; SBB7; RET# S1=0, 'X'=READ DATA, S1=1 THEN RETURN
190# *** PRINT ERROR MESSAGE ***  ;
195 PRI "ERROR DETECTED AT:~"# INFORM USER OF ERROR IN TESTING
200 PRI "A[15:8]="; PRX U# INFORM USER OF UPPER ADDRESS
205 PRI "A[ 7:0]="; PRX L# INFORM USER OF LOWER ADDRESS
210 PRI "EXPECTED= "; PRX D; PRB D
215 PRI "RESULT  = "; PRX X; PRB X
220 PRINT "~"; RET# INSERT CR/LF BETWEEN ADDRESSES
225 ODA Z; ODB Z; ODC Z; ODD Z; ODE Z; ODF Z; RET# ALL PORTS TO INPUTS
230# *** SETUP TMR2: TCCR2A(@$B0)=$42, TCCR2B(@$B1)=$01,  OCR2A($B3)=$07 ***
235 X:= PEEK 0 $64; X:= X AND $BF; POKE X 0 $64  # TMR2 ENABLE IN PRR0
240 SDB4; POKE 63 0 $B3; POKE $42 0 $B0; POKE 1 0 $B1# SETUP TMR2 AS 1MHZ Z80 CLK
245 SDE2; SBE2; PBE2; RET# GENERATE A RESET PULSE TO CYCLE REGISTERS AND RETURN
