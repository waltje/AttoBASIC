5 REM Z80 RAM R/W TEST
10# REM PD2=IORQ, PD3=MREQ, PD4=WR, PD5=RD
15# REM PF1/PF0 = S1/S0 FOR WS-GEN
20# REM PB0=HC299_CS, PB6=HC299_OE, PB7=HC299_S1
25# REM PB4=Z80CLK, PE2=Z80RST, PD6=Z80BUSRQ
30 SPM 2# SPI TO MODE 2
35 D:= $55; Z:=0# DATA TO WRITE=$55, Z HOLDS '0'
40 GOS 185# INSURE ALL PORTS AS INPUTS
45 GOS 195# START Z80 CLOCK AND INITIATE A Z80 RST
50 SDD6; CBD6# INITIATE Z80 BUSRQ
55 IF 1 = IBE0# WAIT FOR Z80 BUSACK
60 GOTO 55# LOOP TILL Z80 BUSACK
65 ODA $FF; OPA $00# SET PORTA OUTPUT AND ADDRESS A[7:0] =0x00
70 ODC $FF; OPC $00# SET PORTC OUTPUT AND ADDRESS A[15:8] =0x00
75 OPB '11010001; ODB '11010111# SS=1 (SEL HC299), OE=1 (HI-Z), (S0=1/S1=1)
80 ODD '10111100; OPD '11111111# CONTROL PORT TO OUTPUTS, ALL HIGH
85 ODE '00000100; OPE '11111111# SET PORTE, INPUTS W/ PULL-UPS
90 ODF '00000011; OPF '11111100# SET S0/S1 OUTPUTS FOR 0 WS
95 RTI 2; RTR# SET 100MS RTC INTERVAL AND RESET THE RTC
100 FOR U=0 255; FOR L=0 255# SET UP THE UPPER AND LOWER ADDRESS LOOPS
105 OPC U; OPA L# OUTPUT CURRENT ADDRESS ONTO BUS
110 PRI "A[15:8]="; PRX U# INFORM USER OF UPPER ADDRESS
115 PRI "A[ 7:0]="; PRX L# INFORM USER OF LOWER ADDRESS
120 GOS 160# WRITE DATA TO THE RAM ADDRESS
125 GOS 175; PRI "DATA= "; PRX X# READ DATA FROM THE RAM ADDRESS AND PRINT IT
130 SBD5; SBD3# RAISE THE Z80 RD AND MREQ SIGNAL
135 PRINT "~"#INSERT CR/LF BETWEEN ADDRESSES
140 D:= COM D# COMPLIMENT DATA TO WRITE (ODD/EVEN ADDR)
145 NEXT L; NEXT U# CONTINUE INCREMENT LOOP
150 END
155# *** WRITE DATA TO DATA BUS ***
160 CBB7; SPW D; CBB6; CBD3# SHIFT ENABLE, WRITE DATA='D', ENABLE DATA ONTO BUS, DROP Z80 MREQ
165 PBD4; SBD3; SBB6; SBB7; RET# PULSE Z80 WR, RAISE Z80 MREQ, OE=1,S1=1 THEN RETURN
170# *** READ DATA FROM DATA BUS ***
175 CBD3; CBD5; SPW 0; SBD5; SBD3# DROP Z80 MREQ, DROP Z80 RD, DUMMY WRITE TO LOAD, RAISE Z80 RD, RAISE Z80 MREQ
180 CBB7; X:= SPR; SBB7; RET# S1=0, 'X'=READ DATA, S1=1 THEN RETURN
185 ODA Z; ODB Z; ODC Z; ODD Z; ODE Z; ODF Z; RET# ALL PORTS TO INPUTS
190# *** SETUP TMR2: TCCR2A(@$B0)=$42, TCCR2B(@$B1)=$01,  OCR2A($B3)=$07 ***
195 X:= PEEK 0 $64; X:= X AND $BF; POKE X 0 $64  # TMR2 ENABLE IN PRR0
200 SDB4; POKE 63 0 $B3; POKE $42 0 $B0; POKE 1 0 $B1# SETUP TMR2 AS 1MHZ Z80 CLK
205 SDE2; SBE2; PBE2; RET# GENERATE A RESET PULSE TO CYCLE REGISTERS AND RETURN
