5 REM MC25AA256 SPI EEP TEST
10 SPM 0; SPC 7; SPS# INIT SPI MODE 0
15 PRI "WRITING DATA~"
20 SPW6; SPS# SEND WRTEN COMMAND
25 SPW2; SPW 0 0# SEND WRT CMD + ADDRESS
30 FOR I=0 63# LOOP TO FILL 1 PAGE OF DATA
35 PRX I; SPW I# WRITE DATA
40 NEXT; SPS# END LOOP AND TERMINATE COMMAND
45 RTI 0; RTR# AND RESET RTC @1MS
50 SPW5; A:=1 AND SPR; SPS# SEND READ STATUS AND READ STATUS
55 IF A!=0 THEN GOTO 50# LOOP TILL WRT DONE
60 PRI "WRT DELAY (MS)= "; PRI PEEK VPG@RTC0# PRINT DELAY TIME IN MS
65 PRI "~READING BACK DATA~"
70 SPW3; SPW 0 0# SEND READ CMD + ADDRESS
75 FOR I=0 63# LOOP TO READ BACK 1 PAGE OF DATA
80 PRX SPR# PRINT THE VALUE
85 NEXT; SPS# END LOOP AND TERMINATE COMMAND
